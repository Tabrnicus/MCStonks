plugins {
    id 'java'
    id 'maven'
}

group 'com.nchroniaris'
version '1.0.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// Different base name than the project root, for all artifacts
archivesBaseName = 'mc-stonks'

repositories {

    mavenCentral()

    maven {
        name = "cronapp-repo"
        url = "https://artifactory.cronapp.io/public-release/"
    }

}

dependencies {

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson-extras
    implementation group: 'com.google.code.gson', name: 'gson-extras', version: '2.8.5'

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '16.0.1'

    testImplementation group: 'junit', name: 'junit', version: '4.12'

}
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task fatJar(type: Jar, dependsOn: classes) {

    archiveClassifier.set('FAT')

    // https://stackoverflow.com/a/49284432
    // As far as I understand this creates a jar file with all the dependencies included in it. Since this program is meant to be distributed and used as a standalone jar (Java is the only "dependency" required) I believe it is a reasonable solution.
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

}

// Make the manifest the same across all jar tasks
allprojects {
    tasks.withType(Jar) {
        manifest {
            attributes(
                    'Main-Class': 'com.nchroniaris.mcstonks.core.Main'
            )
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
